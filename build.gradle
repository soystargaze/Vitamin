plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.erosmari'
version = '1.1.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url = "https://repo.extendedclip.com/releases/"
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("me.clip:placeholderapi:2.11.6")
    implementation("com.zaxxer:HikariCP:5.0.1")
    implementation("org.bstats:bstats-bukkit:3.0.2")
    compileOnly("com.github.angeschossen:LandsAPI:7.10.13")
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.13")
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': version
    }
}

tasks.shadowJar {
    archiveClassifier.set('')
    minimize()
    relocate 'org.bstats', 'com.erosmari.vitamin.shaded.bstats'
}

tasks.build {
    dependsOn tasks.shadowJar
}
